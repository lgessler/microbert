<html><body><text id="wikipedia_ug_wikipedia_org__20877.html" ns="0" revid="153072" sourceurl="https://ug.wikipedia.org/wiki/%D9%85%D8%A7%D8%AA%D8%B1%D9%89%D9%83%D9%89%D8%B3_%D8%B1%DB%95%D9%82%DB%95%D9%85%D9%84%D9%89%D9%83_%D9%8A%D8%A7%D9%85%D8%BA%DB%87%D8%B1%D9%89" title="ماترىكىس رەقەملىك يامغۇرى" type="wikipedia">ماترىكىس رەقەملىك يامغۇرى
<p>&lt;&lt;ماترىكىس&gt;&gt; ئىسىملىق كىنودا كومپىيوتېر ئېكرانىدا چىقىدىغان كود يامغۇرى.</p>
<p>ماترىكىس رەقەملىك يامغۇر</p>
<p>ئۆرنەك:</p>
<p>تۆۋەندىكىنى كودنى text ھۆججىتىگە كۆچۈرۈپ، <hi rend="bold">باشقىچىە ساقلاش</hi> (Save As) قىسىمىدىن bat فورماتى بىلەن ساقلاڭ. يەنى <hi rend="bold">New Text Document.txt</hi> ئىسىملىك بىر text ھۆججىتىگە كۆچۈرگەن بولسىڭىز،باشقىچىە ساقلىغان ۋاقىتتا <hi rend="bold">txt.</hi> دېگەن فورمات قىسمىنى <hi rend="bold">bat.</hi> دەپ ئۆزگەرتىپ يېزىپ ساقلاڭ (مەسىلەن <hi rend="bold">Matrix.bat</hi> دەپ ساقلىۋېلىڭ) ئۇ ھۆججەتنى ئىجرا قىلىڭ.</p>
<p>تەبرىكلەر! ئېكراندا كىنودىكى <hi rend="bold">ماترىكىس رەقەملىك يامغۇرىغا</hi> ئوخشىشىپ كېتىدىغان بىر كود يامغۇرى يېغىشقا باشلايدۇ ^_-</p>
:: Matrix Digital Rain Task from Uyghur Wikipedia
:: Batch File Implementation
 
@echo off
setlocal enabledelayedexpansion

rem escape character (for Windows 10 VT100 escape sequences)
rem info: https://docs.microsoft.com/en-us/windows/console/console-virtual-terminal-sequences
for /f %%e in ('echo prompt $e^| cmd') do @set "esc=%%e"

rem set window size
set "col=190"   %== please don't make this too large ==%
set "row=30"   %== please don't make this too large ==%
mode con cols=%col% lines=%row%

rem set up the variables for display
set "rain_length=!random! %% 15"                    %== vertical length of the rain, default is 12 ==%
for /l %%y in (1,1,%col%) do set "disp_col[%%y]= "   %== what to display ==%
for /l %%y in (1,1,%col%) do set "ctr_col[%%y]=0"   %== counter for rain length ==%

rem hide the cursor, and clear the screen
&lt;nul set /p "=%esc%[?25l"
cls
 
:matrix_loop
for /l %%y in (1,1,%col%) do (
    if !ctr_col[%%y]! equ 0 (
        set "disp_col[%%y]= "
    ) else (
        set /a "rnd_digit=!random! %% 10"
        if !ctr_col[%%y]! equ 1 (
            set "disp_col[%%y]=%esc%[97m!rnd_digit!%esc%[32m"
        ) else if !ctr_col[%%y]! equ 2 (
            set "disp_col[%%y]=%esc%[92m!rnd_digit!%esc%[32m"
        ) else (
            set "disp_col[%%y]=!rnd_digit!"
        )
        set /a "ctr_col[%%y]=(!ctr_col[%%y]! + 1) %% (%rain_length% + 1)"
    )
    rem drop rain randomly
    set /a "rnd_drop=!random! %% 25"                  %== relative distance of raind drops,the bigger the wider,default is 20 ==%
    if !rnd_drop! equ 0 set "ctr_col[%%y]=1"
)
set "disp_line=%esc%[32m"
for /l %%y in (1,1,%col%) do set "disp_line=!disp_line!!disp_col[%%y]!"
&lt;nul set /p "=%esc%[1T%esc%[1;1H"   %== scroll down and set cursor position to home ==%
echo(%disp_line%
goto matrix_loop

<p>
<hi rend="bold">مەنبە:</hi></p>
<p>1. https://rosettacode.org/wiki/Matrix_digital_rain</p></text></body></html>